/*
 * EEL script for REAPER v5+
 *
 * Copyright (C) 2016 Przemyslaw Pawelczyk <przemoc@gmail.com>
 *
 * This script is licensed under the terms of the MIT license.
 * https://opensource.org/licenses/MIT
 */
/* version 1.01 */


/// Returns number of moved items.
function PPP_ItemSel_MoveRightByTimeSelLen(proj)
 local (descfmt, desc,
        it_tot,
        ts_beg, ts_end, ts_len,
        i, it_cnt, it, it_beg,
       )
(
	descfmt = "Move selected items (%u) right by time selection length (%f)";

	it_tot = 0;
	ts_beg = ts_end = -1;
	GetSet_LoopTimeRange2(proj, /*isSet*/0, /*isLoop*/0, ts_beg, ts_end, 0);
	ts_len = ts_end - ts_beg;

	it_cnt = CountSelectedMediaItems(proj);
	i = it_cnt;

	Undo_BeginBlock2(proj);
	while (   ((i -= 1) >= 0)
	       && (it = GetSelectedMediaItem(tk, i))
	      ) (
		it_beg = GetMediaItemInfo_Value(it, "D_POSITION");
		SetMediaItemInfo_Value(it, "D_POSITION", it_beg + ts_len);
		it_tot += 1;
	);

	sprintf(desc, descfmt, it_tot, ts_len);
	Undo_EndBlock2(proj, desc, /*UNDO_STATE_ITEMS*/4);

	UpdateArrange();

	it_tot;
);


PPP_ItemSel_MoveRightByTimeSelLen(0);
